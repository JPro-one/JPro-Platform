plugins {
    id 'java-library'
    id 'org.bytedeco.gradle-javacpp-platform' version "$JAVACPP_VERSION"
}

dependencies {
    implementation "com.sandec.jpro:jpro-webapi:$JPRO_VERSION"
    compileOnly "org.bytedeco:javacv-platform:$JAVACV_VERSION"
    api "org.slf4j:slf4j-api:$SLF4J_API_VERSION"
    api "org.json:json:$JSON_VERSION"

    testImplementation "org.junit.jupiter:junit-jupiter-api:$JUNIT_VERSION"
    testImplementation "org.testfx:testfx-junit5:$TESTFX_VERSION"
    testImplementation "org.bytedeco:javacv-platform:$JAVACV_VERSION"
    testImplementation "org.assertj:assertj-core:$ASSERTJ_VERSION"
    testImplementation "org.hamcrest:hamcrest:$HAMCREST_VERSION"
    testRuntimeOnly 'org.bytedeco:flandmark-platform:1.07-1.5.8'
    testRuntimeOnly "ch.qos.logback:logback-classic:$LOGBACK_VERSION"
}

compileJava {
    options.compilerArgs = ["--add-exports", "javafx.base/com.sun.javafx.event=one.jpro.platform.media"]
}

test {
    useJUnitPlatform()
    jvmArgs = [
            "--add-exports", "javafx.base/com.sun.javafx.event=one.jpro.platform.media",
            "--add-exports", "javafx.graphics/com.sun.javafx.application=org.testfx"
    ]
    testLogging {
        events "passed", "skipped", "failed"
        showExceptions true
        showCauses true
        showStackTraces true
        exceptionFormat "full"
    }

    onlyIf { !Boolean.getBoolean("ciTest") }
}

javadoc {
    options {
        encoding = 'UTF-8'
        version = true
        author = true
        addMultilineStringsOption("-add-exports").setValue([
                "javafx.base/com.sun.javafx.event=one.jpro.platform.media"
        ])
        addStringOption('Xdoclint:none', '-quiet')
    }
}

def docArtifact = artifacts.add('archives', new File("$projectDir", 'DOCUMENTATION.md')) {
    type 'mddoc'
    classifier "DOCUMENTATION"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            pom {
                packaging = 'jar'
                name = 'JPro Media'
                description = 'A library for playing audio and video files in JavaFX applications running via JPro server.'
                url = 'https://www.jpro.one'

                developers {
                    developer {
                        id = 'besidev'
                        name = 'Besmir Beqiri'
                        email = 'bb@sandec.de'
                    }
                }
            }
        }
    }
}