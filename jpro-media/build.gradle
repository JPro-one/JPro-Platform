plugins {
    id 'org.bytedeco.gradle-javacpp-platform' version "$JAVACPP_VERSION"
}

dependencies {
    implementation "com.sandec.jpro:jpro-webapi:$JPRO_VERSION"
    compileOnly "org.bytedeco:javacv-platform:$JAVACV_VERSION"
    runtimeOnly 'org.bytedeco:flandmark-platform:1.07-1.5.8'
    api "org.slf4j:slf4j-api:$SLF4J_API_VERSION"
    api "org.json:json:$JSON_VERSION"

    testImplementation "org.testfx:testfx-junit5:$TESTFX_VERSION"
    testImplementation "org.bytedeco:javacv-platform:$JAVACV_VERSION"
}

compileJava {
    options.compilerArgs = ["--add-exports", "javafx.base/com.sun.javafx.event=one.jpro.platform.media"]
}

test {
    jvmArgs = [
            "--add-exports", "javafx.base/com.sun.javafx.event=one.jpro.platform.media",
            "--add-exports", "javafx.graphics/com.sun.javafx.application=testfx.core",
            "--add-opens", "javafx.graphics/com.sun.javafx.application=testfx.core"
    ]

    onlyIf { !Boolean.getBoolean("ciTest") }
}

javadoc {
    options {
        encoding = 'UTF-8'
        version = true
        author = true
        addMultilineStringsOption("-add-exports").setValue([
                "javafx.base/com.sun.javafx.event=one.jpro.platform.media"
        ])
        addStringOption('Xdoclint:none', '-quiet')
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            // Add README.md as an artifact named DOCUMENTATION.md
            artifact(source: file('build/docs/DOCUMENTATION.md')) {
                classifier 'documentation'
                extension 'md'
            }

            pom {
                packaging = 'jar'
                name = 'JPro Media'
                description = 'A library for playing audio and video files in JavaFX applications running natively ' +
                        'and via JPro server.'
                url = 'https://www.jpro.one'

                developers {
                    developer {
                        id = 'besidev'
                        name = 'Besmir Beqiri'
                        email = 'bb@sandec.de'
                    }
                }
            }
        }
    }
}

tasks.register('renameReadme', Copy) {
    from 'README.md'
    into layout.buildDirectory.dir('docs')
    rename 'README.md', 'DOCUMENTATION.md'
}

javadocJar.dependsOn renameReadme
