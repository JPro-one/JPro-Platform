plugins {
    id 'org.bytedeco.gradle-javacpp-platform' version "$JAVACPP_VERSION"
    id 'org.openjfx.javafxplugin'
    id 'jpro-gradle-plugin'
}

dependencies {
    implementation project(":jpro-media")
    implementation project(":jpro-file")
    implementation "com.sandec.jpro:jpro-webapi:$JPRO_VERSION"

    implementation "org.bytedeco:javacv-platform:$JAVACV_VERSION"
//    compileOnly "org.bytedeco:javacv-platform:$JAVACV_VERSION" // when running on JPro
    runtimeOnly 'org.bytedeco:flandmark-platform:1.07-1.5.8' // when running on desktop/device only
    implementation "io.github.mkpaz:atlantafx-base:$ATLANTAFX_VERSION"
    runtimeOnly "ch.qos.logback:logback-classic:$LOGBACK_VERSION"
}

javafx {
    version = "$JAVAFX_VERSION"
    modules = ['javafx.controls', 'javafx.swing', 'javafx.media']
}

def examples = [
        'media-player' : 'one.jpro.platform.media.example.MediaPlayerSample',
        'media-recorder' : 'one.jpro.platform.media.example.MediaRecorderSample',
        'media-recorder-and-player' : 'one.jpro.platform.media.example.MediaRecorderAndPlayerSample'
]

mainClassName = project.hasProperty("sample") ? examples[project.getProperties().get("sample")] : examples["media-recorder-and-player"]

run {
    doFirst {
        jvmArgs = [
                '--add-exports', 'javafx.base/com.sun.javafx.event=one.jpro.platform.media'
        ]
    }
}

application {
  mainClass = "$mainClassName"
  applicationDefaultJvmArgs = [
          "--add-exports", "javafx.base/com.sun.javafx.event=one.jpro.platform.media"
  ]
}
