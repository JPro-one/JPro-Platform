plugins {
    id 'org.bytedeco.gradle-javacpp-platform' version "$JAVACPP_VERSION"
    id 'org.openjfx.javafxplugin'
    id 'jpro-gradle-plugin'
}

ext {
    // Add more needed platforms here
    javacppPlatform = 'linux-x86_64,linux-arm64,macosx-x86_64,macosx-arm64,windows-x86_64'
}

dependencies {
    implementation project(":jpro-media")
    implementation project(":jpro-file")
    implementation "one.jpro:jpro-webapi:$JPRO_VERSION"
    implementation "org.slf4j:slf4j-api:$SLF4J_API_VERSION"
    runtimeOnly "ch.qos.logback:logback-classic:$LOGBACK_VERSION"

    implementation "org.bytedeco:javacv-platform:$JAVACV_VERSION"
//    compileOnly "org.bytedeco:javacv-platform:$JAVACV_VERSION" // when running on JPro
    implementation "io.github.mkpaz:atlantafx-base:$ATLANTAFX_VERSION"
}

javafx {
    version = "$JAVAFX_EXAMPLES_VERSION"
    modules = ['javafx.controls', 'javafx.swing', 'javafx.media']
}

def examples = [
        'media-player'             : 'one.jpro.platform.media.example.MediaPlayerSample',
        'media-recorder'           : 'one.jpro.platform.media.example.MediaRecorderSample',
        'media-recorder-and-player': 'one.jpro.platform.media.example.MediaRecorderAndPlayerSample'
]

application {
    mainClass = project.hasProperty("sample") ? examples[project.getProperties().get("sample")] : examples["media-recorder-and-player"]
    mainModule = 'one.jpro.platform.media.example'
    applicationDefaultJvmArgs = [
            "--add-exports", "javafx.base/com.sun.javafx.event=one.jpro.platform.media"
    ]
}
