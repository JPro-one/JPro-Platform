buildscript {
  repositories {
    gradlePluginPortal()
    maven {
        url "https://sandec.jfrog.io/artifactory/repo"
    }
  }
  dependencies {
    classpath 'org.openjfx:javafx-plugin:0.0.13'
    classpath "com.sandec.jpro:jpro-plugin-gradle:$JPRO_VERSION"
  }
}

allprojects {
  repositories {
    jcenter()
    mavenCentral()
    maven {
      url "https://sandec.jfrog.io/artifactory/repo"
    }
  }
  

  apply plugin: 'java'
  apply plugin: 'java-library'
  apply plugin: 'org.openjfx.javafxplugin'

  compileJava {
    sourceCompatibility = 11
    targetCompatibility = 11
  }

  javafx {
    version = "$JAVAFX_VERSION"
    configuration = "compileOnly"
    modules = [ 'javafx.base', 'javafx.graphics', 'javafx.controls', 'javafx.fxml', 'javafx.media', 'javafx.web' ]
  }

  dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    testImplementation "de.sandec:JMemoryBuddy:0.5.1"
  }

  test {
    useJUnitPlatform()
    testLogging.showStandardStreams = true
    testLogging.exceptionFormat = "full"
  }
}

configure([project("tree-showing"), project("jpro-sound"), project("jpro-sessions"), project("htmlscrollpane")]) {
  apply plugin: 'maven-publish'
  publishing {
    publications {
      mavenJava(MavenPublication) {
        from components.java
        groupId "one.jpro.jproutils"
        artifactId project.name
        version JFX_UTILS_VERSION
      }
    }
    repositories {
      maven {
        name = "artifactory"
        url = "https://sandec.jfrog.io/artifactory/repo"
        credentials {
          username System.getenv("SANDEC_ARTIFACTORY_USERNAME")
          password System.getenv("SANDEC_ARTIFACTORY_PASSWORD")
        }
      }
    }
  }
}