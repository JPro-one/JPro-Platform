buildscript {
  repositories {
    gradlePluginPortal()
    maven {
        url "https://sandec.jfrog.io/artifactory/repo"
    }
  }
  dependencies {
    classpath 'org.openjfx:javafx-plugin:0.0.14-SNAPSHOT-FORK'
    classpath "one.jpro:jpro-gradle-plugin:$JPRO_VERSION"
  }
}

configure(subprojects.findAll {it.name != 'example'}) {
  repositories {
    jcenter()
    mavenCentral()
    maven {
      url "https://sandec.jfrog.io/artifactory/repo"
    }
  }

  apply plugin: 'java'
  apply plugin: 'java-library'
  apply plugin: 'org.openjfx.javafxplugin'

  compileJava {
    sourceCompatibility = 17
    targetCompatibility = 17
  }

  javafx {
    version = "$JAVAFX_VERSION"
    configurations = ["compileOnly", "testImplementation"]
    modules = [ 'javafx.base', 'javafx.graphics', 'javafx.controls', 'javafx.fxml',
                'javafx.media', 'javafx.swing','javafx.web' ]
  }

  dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    testImplementation "de.sandec:JMemoryBuddy:0.5.1"
  }

  test {
    useJUnitPlatform()
    testLogging.showStandardStreams = true
    testLogging.exceptionFormat = "full"
  }
}

configure([project("jpro-sessions")]) {
  dependencies {
    implementation "com.sandec.jpro:jpro-webapi:$JPRO_VERSION"
    implementation group: 'commons-io', name: 'commons-io', version: '2.11.0'
  }
}
configure([project("tree-showing"), project("jpro-sound"), project("jpro-sessions"), project("htmlscrollpane"), project("freeze-detector")]) {
  apply plugin: 'maven-publish'
  publishing {
    publications {
      mavenJava(MavenPublication) {
        from components.java
        groupId "one.jpro.jproutils"
        artifactId project.name
        version JFX_UTILS_VERSION
      }
    }
    java {
      withSourcesJar()
      withJavadocJar()
    }
    repositories {
      maven {
        name = "artifactory"
        url = "https://sandec.jfrog.io/artifactory/repo"
        credentials {
          username System.getenv("SANDEC_ARTIFACTORY_USERNAME")
          password System.getenv("SANDEC_ARTIFACTORY_PASSWORD")
        }
      }
    }
  }
}